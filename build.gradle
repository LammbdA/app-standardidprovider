plugins {
    id 'java'
    id 'maven'
    id 'com.enonic.defaults' version '1.0.3'
    id 'com.enonic.xp.app' version '1.0.17'
    id "com.moowork.node" version "1.2.0"
}

apply from: "$rootDir/gradle/defaults.gradle"

repositories {
    jcenter()
    xp.enonicRepo()
}

app {
    name = 'com.enonic.xp.app.standardidprovider'
    group = 'com.enonic.xp'
    displayName = 'Standard ID Provider'
    systemApp = true
    systemVersion = "${xpVersion}"
}

dependencies {
    include "${group}:lib-admin:${xpVersion}"
    include "${group}:lib-auth:${xpVersion}"
    include "${group}:lib-context:${xpVersion}"
    include "${group}:lib-mustache:${xpVersion}"
    include "${group}:lib-portal:${xpVersion}"
    include "${group}:lib-node:${xpVersion}"
}

//──────────────────────────────────────────────────────────────────────────────
// Gradle node plugin
//──────────────────────────────────────────────────────────────────────────────
node {
    version '8.9.4'
    download true
}

task webpack( type: NodeTask, dependsOn: npmInstall ) {
    description = 'Bundle resources'
    inputs.dir 'src/main'
    outputs.dir "${buildDir}/resources/main"
    script = file( './node_modules/webpack/bin/webpack' )
}

jar.dependsOn 'webpack'
jar.outputs.dir "${buildDir}/resources/main"
